
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


test {
    useJUnitPlatform()
}


dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2' //for the OverridingMethodsMustInvokeSuper annotation

    /*
     * It is presumed any user of Tamarin will already be using JMonkey but we don't want to force a version on them.
     * That is why this is a compileOnly dependency
     */
    compileOnly libs.jme3.core
    compileOnly libs.jme3.desktop
    /*
     * Lemur is an optional dependency
     */
    compileOnly ("com.simsilica:lemur:1.16.0")
    compileOnly ("com.simsilica:lemur-proto:1.13.0")

    /*
     * Minie is an optional dependency
     */
    compileOnly 'com.github.stephengold:Minie:8.2.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation libs.jme3.core
    testImplementation 'org.mockito:mockito-core:5.4.0'
}

publishing { //see https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'tamarin-core'
            version = "${version}"

            from components.java

            pom {
                name = project.name
                description = 'The part of Tamarin that is shared by both the android and desktop modules'
                url = 'https://github.com/oneMillionWorlds/Tamarin'
                licenses {
                    license {
                        name = 'New BSD (3-clause) License'
                        url = 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    developerConnection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    url = 'https://github.com/oneMillionWorlds/Tamarin'
                }
                developers {
                    developer {
                        id = 'RichardTingle'
                        name = 'Richard Tingle (aka richtea)'
                        email = 'support@oneMillionWorlds.com'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'CentralPortalStaging'
            url = uri("${rootProject.buildDir}/central-portal-staging")
        }
        // Legacy Sonatype repo retained for local testing only; CI no longer uses it
        maven {
            name 'sonatype'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            //url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username findProperty('ossrhUsername') ?: ''
                password findProperty('ossrhPassword') ?: ''
            }
        }
    }
}

signing {
    // Fallback: inject signing.keyId from environment if not provided via properties
    if (!project.hasProperty('signing.keyId')) {
        def envKeyId = System.getenv('SIGNING_KEY_ID')
        if (envKeyId && envKeyId.trim()) {
            project.ext.set('signing.keyId', envKeyId.trim())
            logger.lifecycle("[Signing Debug] Applied signing.keyId from SIGNING_KEY_ID env var")
        } else {
            logger.lifecycle("[Signing Debug] No SIGNING_KEY_ID env var found")
        }
    }
    def ringFileProp = findProperty('signing.secretKeyRingFile')
    def keyId = findProperty('signing.keyId')
    def passSet = project.hasProperty('signing.password')
    if (ringFileProp) {
        def f = file(ringFileProp)
        logger.lifecycle("[Signing Debug] secretKeyRingFile='${f}' exists=${f.exists()} keyIdSet=${keyId != null} passSet=${passSet}")
    } else {
        logger.lifecycle("[Signing Debug] secretKeyRingFile is NOT set. keyIdSet=${keyId != null} passSet=${passSet}")
    }
    sign publishing.publications.mavenJava
}
