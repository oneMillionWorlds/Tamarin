
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.lombok" version "8.4"
}

repositories {
    jcenter()
    mavenCentral()
}

java {
    withJavadocJar()
    //withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.register('sourcesJar', Jar) {
    // Replace the source set with the delomboked files
    from sourceSets.main.delombokTask
    archiveClassifier.set('sources')

}

tasks.sourcesJar.dependsOn delombok

group = 'com.onemillionworlds'
version = '2.6.0-beta1'

ext{
    lwjgl3Version = '3.3.3' //it is important that this is the same as JME which provides the rest of LWJGL
}
test {
    useJUnitPlatform()
}


def jme3 = [v:'3.7.0-beta1', g:'org.jmonkeyengine']

dependencies {

    api "org.lwjgl:lwjgl-openxr:${lwjgl3Version}"

    runtimeOnly "org.lwjgl:lwjgl-openxr:${lwjgl3Version}:natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-openxr:${lwjgl3Version}:natives-linux"
    api "org.lwjgl:lwjgl-egl:${lwjgl3Version}" // Necessary for the cross platform XrGraphicsBindingEGLMNDX binding

    implementation 'com.google.code.findbugs:jsr305:3.0.2' //for the OverridingMethodsMustInvokeSuper annotation

    /*
     * It is presumed any user of Tamarin will already be using JMonkey but we don't want to force a version on them.
     * That is why this is a compileOnly dependency
     */
    compileOnly "${jme3.g}:jme3-core:${jme3.v}"
    compileOnly "${jme3.g}:jme3-lwjgl3:${jme3.v}"
    compileOnly "${jme3.g}:jme3-desktop:${jme3.v}"
    /*
     * Lemur is an optional dependency
     */
    compileOnly ("com.simsilica:lemur:1.16.0")
    compileOnly ("com.simsilica:lemur-proto:1.13.0")
    /*
     * Lombok is only required during compile (it's an annotation processor)
     */
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation "${jme3.g}:jme3-core:${jme3.v}"
}

publishing { //see https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'tamarin'
            version = "${version}"

            from components.java

            pom {
                name = project.name
                description = 'A VR utilities library that extends the functionality in JMonkeyEngine'
                url = 'https://github.com/oneMillionWorlds/Tamarin'
                licenses {
                    license {
                        name = 'New BSD (3-clause) License'
                        url = 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    developerConnection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    url = 'https://github.com/oneMillionWorlds/Tamarin'
                }
                developers {
                    developer {
                        id = 'RichardTingle'
                        name = 'Richard Tingle (aka richtea)'
                        email = 'support@oneMillionWorlds.com'
                    }
                }
            }

            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name 'sonatype'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            //url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username findProperty('ossrhUsername') ?: ''
                password findProperty('ossrhPassword') ?: ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
