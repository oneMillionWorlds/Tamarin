
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.lombok" version "6.4.0"
}

repositories {
    jcenter()
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

group = 'com.onemillionworlds'
version = '1.2-SNAPSHOT'

def jme3 = [v:'3.4.0-stable', g:'org.jmonkeyengine']

dependencies {
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13.2'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    //api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    //implementation 'com.google.guava:guava:30.1.1-jre'

    /*
     * It is presumed any user of Tamarin will already be using JMonkey but we don't want to force a version on them.
     * That is why this is a compileOnly dependency
     */
    compileOnly "${jme3.g}:jme3-core:${jme3.v}"
    compileOnly "${jme3.g}:jme3-vr:${jme3.v}"
    compileOnly "com.simsilica:lemur:1.14.0"
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
}

publishing { //see https://docs.gradle.org/current/userguide/publishing_maven.html
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'tamarin'
            version = "${version}"

            from components.java

            pom {
                name = project.name
                description = 'A VR utilities library that extends the functionality in JMonkeyEngine'
                url = 'https://github.com/oneMillionWorlds/Tamarin'
                licenses {
                    license {
                        name = 'New BSD (3-clause) License'
                        url = 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                scm {
                    connection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    developerConnection = 'git@github.com:oneMillionWorlds/Tamarin.git'
                    url = 'https://github.com/oneMillionWorlds/Tamarin'
                }
                developers {
                    developer {
                        id = 'RichardTingle'
                        name = 'Richard Tingle (aka richtea)'
                        email = 'support@oneMillionWorlds.com'
                    }
                }
            }

        }
    }
    repositories {
        maven {
            name 'sonatype'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            //url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}